# El gametype es el nombre de juego que usaremos!
gametype:    Ajedrez

# Cada color se define aparte o no
color:       blanco 
color:       negro 

# Board se puede definir como una lista de dimensiones.
# Tambien podria estar en blanco y comenzar con las definiciones
# de casillero de una.
board:       "a b c d e f g h" "1 2 3 4 5 6 7 8"

# Directions tiene el nombre de la direccion. Opcionalmente podria
# tener n numeros que hace la representacion 'n' dimensional.
directions:  n  0  1 
directions:  s  0 -1 
directions:  e  1  0  
directions:  o -1  0 

# Simetria ... un color y dos direcciones
symmetry:    negro   s    n
symmetry:    negro   n    s

# Zona. Tiene el nombre de zona (obligatorio), luego
# cero, uno o mas colores, indicando a que colores aplica y finalmente
# una lista de casilleros.
zone:        peones  blanco  a3  b3  c3  d3  e3  f3  g3  h3
zone:        peones  negro   a6  b6  c6  d6  e6  f6  g6  h6
zone:        coronacion  blanco  a8  b8  c8  d8  e8  f8  g8  h8
zone:        coronacion  negro   a1  b1  c1  d1  e1  f1  g1  h1

movetype:    enroque_corto
movetype:    enroque_largo

# Toda esta parte tiene que ver con
# las definiciones para hacer mas facil 
# la generacion de los miniprogramas
# de movimiento
#
# El primer define
# 
define:      movida_derecho direccion 
             direccion
             while en_tablero? && !ocupado_propio?  do
                captura_y_juega
                parar if ocupado_enemigo?
                direccion
             end

define:     movida_torre direccion 
            direccion
            while en_tablero? && !ocupado_propio?  do
              asigna_atributo  ya_movido , true
              captura_y_juega
              parar if ocupado_enemigo?
              direccion
            end

define:     movida_caballo direccion
            direccion
            parar if !en_tablero?
            captura_y_juega if !ocupado_propio?

define:     movida_rey direccion
            direccion
            parar if !en_tablero?
            parar if ocupado_propio?
            parar if atacado_enemigo?
            asigna_atributo 'ya_movido', true
            captura_y_juega

define:     al_paso direccion
            direccion
            parar if !ocupado_enemigo?
            parar if !destino_anterior?
            parar if !ocupado? peon 
            n; n
            parar if !origen_anterior?
            casillero_inicial
            direccion
            captura
            n
            juega

# Listo, ahora si ... se vienen las definiciones de piezas
#
piece:      torre
attribute:  ya_movido   false
move:       
            movida_torre n
move:       
            movida_torre s
move:       
            movida_torre e
move:       
            movida_torre o

piece:      alfil
move:       
            movida_derecho "n; e"
move:       
            movida_derecho "s; e"
move:       
            movida_derecho "s; o"
move:       
            movida_derecho "n; o"

piece:      dama
move:       
            movida_derecho "n"
move:       
            movida_derecho "s"
move:       
            movida_derecho "o"
move:       
            movida_derecho "e"
move:       
            movida_derecho "n; e"
move:       
            movida_derecho "s; e"
move:       
            movida_derecho "s; o"
move:       
            movida_derecho "n; o"

piece:      caballo
move:       
            movida_caballo 'n; n; e'
move:       
            movida_caballo 'n; n; o'
move:       
            movida_caballo 's; s; e'
move:       
            movida_caballo 's; s; o'
move:       
            movida_caballo 'e; e; s'
move:       
            movida_caballo 'o; o; s'
move:     
            movida_caballo 'e; e; n'
move:      
            movida_caballo 'o; o; n' 


piece:      "peon"
# Movida para adelante
move:       
            n
            parar if !en_tablero?
            parar if ocupado? 
            if  en_zona? coronacion
               transforma 'caballo', 'alfil', 'torre', 'dama'
            end
            juega
            if en_zona? 'peones'
              n; juega if !ocupado?
            end

# Captura del peon
move:
            n; e; 
            if ocupado_enemigo? 
              if en_zona? coronacion
                transforma 'caballo', 'alfil', 'torre', 'dama'
              end
              captura_y_juega
            end
            casillero_inicial; n; o;
            if ocupado_enemigo? 
              if en_zona? coronacion
                transforma 'caballo', 'alfil', 'torre', 'dama'
              end
              captura_y_juega
            end

# Al paso
move:
            al_paso "e" 
move:
            al_paso "o" 

piece:      rey            
attribute:  ya_movido   false
move:
            movida_rey "n" 
move:
            movida_rey "s"
move:
            movida_rey "e"
move:
            movida_rey "o"
move:
            movida_rey "n; e;"
move:
            movida_rey "n; o;"
move:
            movida_rey "s; o;"
move:
            movida_rey "s; e;"


move:       enroque_corto
            parar if ya_movido?
            e; parar if ocupado?; parar if atacado_enemigo?
            marca aqui
            e; parar if ocupado?; parar if atacado_enemigo?
            e; parar if ocupado_enemigo?;
            parar if !ocupado? torre; 
            parar if ya_movido?;
            mueve piezas_en_casillero marca
            casillero_inicial; e; e; 
            asigna_atributo 'ya_movido', true
            juega

move:       enroque_largo
            parar if ya_movido?
            o; parar if ocupado?; parar if atacado_enemigo?
            marca aqui
            o; parar if ocupado?; parar if atacado_enemigo?
            o; parar if ocupado?
            o; parar if ocupado_enemigo?; 
            parar if !ocupado? torre; 
            parar if ya_movido?;
            mueve piezas_en_casillero marca
            casillero_inicial; o; o; 
            asigna_atributo 'ya_movido', true
            juega

# Listo con la definicion de piezas ... mas cosas
#
# Se vienen los finales. Son tambien distintos mini programas!
ending:    
            gana if jaquemate? "rey"
ending:     
            empata if ahogado?
ending:
            empata if repite_posicion? 3 
ending:
            empata if cantidad_piezas? 2
            parar  if !cantidad_piezas? 3
            empata if cantidad_piezas? alfil = 1
            empata if cantidad_piezas? caballo = 1

sequence:   blanco negro
start:      torre    blanco  a1  h1
start:      torre    negro   a8  h8
start:      caballo  blanco  b1  g1
start:      caballo  negro   b8  g8
start:      alfil    blanco  c1  f1
start:      alfil    negro   c8  f8
start:      dama     blanco  d1
start:      dama     negro   d8
start:      rey      blanco  e1
start:      rey      negro   e8
start:      peon     blanco  a2  b2  c2  d2
start:      peon     blanco  e2  f2  g2  h2
start:      peon     negro   a7  b7  c7  d7
start:      peon     negro   e7  f7  g7  h7
  

notation:   rey     K
notation:   dama    Q
notation:   torre   R
notation:   alfil   B
notation:   caballo N
notation:   peon    ""
notation:   captured-mark   x
notation:   mark           "-"
notation:   default   piece-name  captured-mark      target
notation:   onrepeat  piece-name  origin             mark    target         
notation:   enroque_corto   "0-0"
notation:   enroque_largo   "0-0-0"

# Aca vienen las definiciones de tablero ... simplemente voy a hacer
# un damero de dimensiones estandares
graph-board: checkerboard   384 384    $FFFFFF $A0A0A0 
graph-piece: standard-king     rey      48 48
graph-piece: standard-queen    dama     48 48
graph-piece: standard-bishop   alfil    48 48
graph-piece: standard-knight   caballo  48 48
graph-piece: standard-rook     torre    48 48
graph-piece: standard-pawn     peon     48 48

logo: e4 e5 f4

